function void BlueSpheresTimer.Menu.medals.headline()
{
	s32 offset = 146
	s32 px = getScreenWidth() / 0x02 - offset
	
	for (u8 A = 0x00; A < 0x04; ++A)
		BlueSpheresTimer.TextManager.drawText(px + (offset * A) - ((level.framecounter * 0x02) % offset), 25, 0xf000, 0x01, STRING_MEDALS_HEADLINE, 0x00)
}

function void BlueSpheresTimer.Menu.medals.items()
{
	if (bluespheres.popup.show && SETTING_BS_MENU_OPTIMIZATION)
		return
	
	s32 px = getScreenWidth() / 0x02
	s32 py = 46
	
	BlueSpheresTimer.Menu.medals.items.menu(px, py)
	BlueSpheresTimer.Menu.medals.items.medals(px, py)
}

function void BlueSpheresTimer.Menu.medals.items.menu(s32 px, s32 py)
{
	string button_back = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.medals.buttonselected, bluespheres.buttons.setup, STRING_BACK)
	BlueSpheresTimer.TextManager.drawText(px, getScreenHeight() - py, 0xf000, 0x01, button_back, 0x01)
}

function void BlueSpheresTimer.Menu.medals.items.medals(s32 px, s32 py)
{
	if (bluespheres.medals.buttonselected >= 0x01 && !(bluespheres.medals.buttonselected >= bluespheres.buttons.setup))
	{
		for (u8 A = 0x00; A <= bluespheres.medals.buttonselected - 0x01; ++A)
			py -= 76
		
		if (bluespheres.medals.buttonselected >= 0x02)
		{
			for (u8 A = 0x00; A <= bluespheres.medals.buttonselected - 0x02; ++A)
				py -= 52 // = 128 - 76
		}
	}
	
	px -= 164
	py += 0x08
	for (u8 A = 0x00; A < bluespheres.buttons.setup; ++A)
	{
		if (A >= 0x02)
			py += 128
		else if (A != 0x00)
			py += 76
		
		if !(py >= getScreenHeight() || py <= 0x00)
		{
			u8 alpha = (py < 54 || py > getScreenHeight() - 54) ? 48 : ((py >= 130) ? 150 : 255)
			
			if (A == 0x00)
				BlueSpheresTimer.Menu.medals.items.tableAchievements(px, py, 0xf000, alpha)
			else if (A == 0x01)
				BlueSpheresTimer.Menu.medals.items.tableRecords(px, py, 0xf000, CHARACTER_SONIC, alpha)
			else if (A == 0x02)
				BlueSpheresTimer.Menu.medals.items.tableRecords(px, py, 0xf000, CHARACTER_TAILS, alpha)
			else if (A == 0x03)
				BlueSpheresTimer.Menu.medals.items.tableRecords(px, py, 0xf000, CHARACTER_KNUCKLES, alpha)
		}
	}
}

function void BlueSpheresTimer.Menu.medals.items.tableRecords(s32 px, s32 py, u16 renderQueue, u8 character, u8 alpha)
{
	bool japanese = !(global.region_code & 0x80)
	string sign_title = stringformat("%s (%s)", STRING_TIME_ATTACK_RECORDS, character == CHARACTER_SONIC ? STRING_CHARACTER_SONIC : (character == CHARACTER_TAILS ? (japanese ? STRING_CHARACTER_MILES : STRING_CHARACTER_TAILS) : (character == CHARACTER_KNUCKLES ? STRING_CHARACTER_KNUCKLES : STRING_RETRY)))
	BlueSpheresTimer.Menu.medals.items.tableTitle(px, py, renderQueue, sign_title, alpha)
	
	px += 0x07
	py += 32
	for (u8 A = 0x01; A <= 0x07; ++A)
	{
		// - Unfinished yet
		// Draw medal sprite
		Renderer.drawCustomSprite("icon_medal_lock", px, py, 0x40, 0x00, renderQueue, 0x00, alpha)
		
		// Offset for the next object
		px += 46
	}
	
	px -= (46 * 0x07)
	py += 48
	for (u8 A = 0x01; A <= 0x07; ++A)
	{
		// - Unfinished yet
		// Draw medal sprite
		Renderer.drawCustomSprite("icon_medal_lock", px, py, 0x40, 0x00, renderQueue, 0x00, alpha)
		
		// Offset for the next object
		px += 46
	}
}

function void BlueSpheresTimer.Menu.medals.items.tableAchievements(s32 px, s32 py, u16 renderQueue, u8 alpha)
{
	BlueSpheresTimer.Menu.medals.items.tableTitle(px, py, renderQueue, STRING_TIME_ATTACK_ACHIEVEMENTS, alpha)
	
	px += 0x07
	py += 32
	for (u8 A = 0x01; A <= 0x08; ++A)
	{
		// Draw border
		if ((bluespheres.medals.medalselected == A) && (bluespheres.medals.buttonselected == 0x00))
			Renderer.drawCustomSprite("icon_medal_borderBlock", px, py, 0x40, 0x00, renderQueue, 0x00, alpha)
		
		// Draw medal sprite
		Renderer.drawCustomSprite(BlueSpheresTimer.Medals.isUnlocked(0x00, A) ? BlueSpheresTimer.Medals.getSprite(0x00, A) : "icon_medal_lock", px, py, 0x40, 0x00, renderQueue, 0x00, alpha)
		
		// Offset for the next object
		px += 40
	}
}

function void BlueSpheresTimer.Menu.medals.items.tableTitle(s32 px, s32 py, u16 renderQueue, string text, u8 alpha)
{
	BlueSpheresTimer.TextManager.drawText(px - 0x06, py, renderQueue, 0x02, "*", 0x01, alpha)
	
	px += 16
	BlueSpheresTimer.TextManager.drawTextWithLine(px, py, renderQueue, 0x03, text, 0x01, alpha)
}

function void BlueSpheresTimer.Menu.medals.updateControl()
{
	// Ignore if popup menu active
	if (bluespheres.popup.show)
		return
	
	// Button selection
	if (control.pad1.pressed & (CONTROL_UP | CONTROL_DOWN))
	{
		playSound(0x5b)
		bluespheres.medals.medalselected = 0x01
		
		if (control.pad1.pressed & CONTROL_UP)
		{
			if (bluespheres.medals.buttonselected != 0x00)
				--bluespheres.medals.buttonselected
			else
				bluespheres.medals.buttonselected = bluespheres.buttons.setup
		}
		else if (control.pad1.pressed & CONTROL_DOWN)
		{
			if (bluespheres.medals.buttonselected < bluespheres.buttons.setup)
				++bluespheres.medals.buttonselected
			else
				bluespheres.medals.buttonselected = 0x00
		}
		
		if (bluespheres.medals.buttonselected == 0x00)
			bluespheres.medals.medals.setup = 0x08
	}
	
	// Button selection
	if (!(bluespheres.medals.buttonselected >= bluespheres.buttons.setup) && (control.pad1.pressed & (CONTROL_LEFT | CONTROL_RIGHT)))
	{
		playSound(0x5b)
		if (control.pad1.pressed & CONTROL_LEFT)
		{
			if (bluespheres.medals.medalselected != 0x01)
				--bluespheres.medals.medalselected
			else
				bluespheres.medals.medalselected = bluespheres.medals.medals.setup
		}
		else if (control.pad1.pressed & CONTROL_RIGHT)
		{
			if (bluespheres.medals.medalselected < bluespheres.medals.medals.setup)
				++bluespheres.medals.medalselected
			else
				bluespheres.medals.medalselected = 0x01
		}
	}
	
	// Perform the action of selected button when start pressed
	if (control.pad1.pressed & CONTROL_START)
	{
		if (bluespheres.medals.buttonselected == bluespheres.buttons.setup)
		{
			playSound(0x63)
			global.game_mode = 0x88
		}
		
		for (u8 A = 0x01; A <= bluespheres.medals.medals.setup; ++A)
		{
			if (bluespheres.medals.medalselected == A && (bluespheres.medals.buttonselected < bluespheres.buttons.setup))
			{
				if (BlueSpheresTimer.Medals.isUnlocked(bluespheres.medals.buttonselected, A))
				{
					playSound(0x63)
					bluespheres.popup.show = 0x01
					waitForNextFrame()
				}
				else
				{
					playSound(0x7b)
				}
			}
		}
	}
	
	// Go back to the achievements menu
	if (control.pad1.pressed & CONTROL_B)
	{
		playSound(0xad)
		global.game_mode = 0x88
	}
}

function void BlueSpheresTimer.Menu.medals.popups()
{
	if !(bluespheres.popup.show)
		return
		
	if (control.pad1.pressed & CONTROL_START)
	{
		playSound(0x63)
		bluespheres.popup.show = 0x00
	}
	
	if (bluespheres.medals.buttonselected == 0x00)
	{
		if (bluespheres.medals.medalselected == 0x01)
		{
			BlueSpheresTimer.Menu.popup.show(STRING_MEDAL_BLUE_SPHERE.LINE1, STRING_MEDAL_BLUE_SPHERE.LINE2, "", "", STRING_CLOSE)
		}
		else if (bluespheres.medals.medalselected == 0x02)
		{
			BlueSpheresTimer.Menu.popup.show(STRING_MEDAL_OH_SHINY.LINE1, STRING_MEDAL_OH_SHINY.LINE2, "", "", STRING_CLOSE)
		}
		else if (bluespheres.medals.medalselected == 0x03)
		{
			BlueSpheresTimer.Menu.popup.show(STRING_MEDAL_LIKE_JUMPING.LINE1, STRING_MEDAL_LIKE_JUMPING.LINE2, "", "", STRING_CLOSE)
		}
		else if (bluespheres.medals.medalselected == 0x04)
		{
			BlueSpheresTimer.Menu.popup.show(STRING_MEDAL_MY_POWER.LINE1, "", "", "", STRING_CLOSE)
		}
		else if (bluespheres.medals.medalselected == 0x05)
		{
			BlueSpheresTimer.Menu.popup.show(STRING_MEDAL_DAGE.LINE1, STRING_MEDAL_DAGE.LINE2, "", "", STRING_CLOSE)
		}
		else if (bluespheres.medals.medalselected == 0x06)
		{
			BlueSpheresTimer.Menu.popup.show(STRING_MEDAL_CHALLENGES.LINE1, STRING_MEDAL_CHALLENGES.LINE2, "", "", STRING_CLOSE)
		}
		else if (bluespheres.medals.medalselected == 0x07)
		{
			BlueSpheresTimer.Menu.popup.show(STRING_MEDAL_FEATURES.LINE1, STRING_MEDAL_FEATURES.LINE2, "", "", STRING_CLOSE)
		}
		else if (bluespheres.medals.medalselected == 0x08)
		{
			BlueSpheresTimer.Menu.popup.show(STRING_MEDAL_LONGPLAY.LINE1, "", "", "", STRING_CLOSE)
		}
	}
}