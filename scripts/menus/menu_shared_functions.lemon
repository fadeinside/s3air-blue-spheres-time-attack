function void BlueSpheresTimer.Menu.shared.headline()
{
	s32 offset = (bluespheres.timer.currentTime >= 50000) ? 424 : 200
	s32 px = getScreenWidth() / 0x02 - offset
	
	// Flashing when a new record
	if ((BlueSpheresTimer.Records.getRecordTime(0x01) == bluespheres.timer.currentTime) && (level.framecounter + 16) & 32 && !(level.framecounter >= 236))
		return
	
	string headline
	if (BlueSpheresTimer.Records.getRecordTime(0x01) == bluespheres.timer.currentTime)
		headline = STRING_SHARED_BEST_TIME
	else if (BlueSpheresTimer.Records.getRecordTime(0x02) == bluespheres.timer.currentTime)
		headline = STRING_SHARED_GOOD_GAME
	else if (BlueSpheresTimer.Records.getRecordTime(0x03) == bluespheres.timer.currentTime)
		headline = STRING_SHARED_CAN_BETTER
	else if (BlueSpheresTimer.Records.getRecordTime(0x04) == bluespheres.timer.currentTime)
		headline = STRING_SHARED_NICE_TRY
	else if (bluespheres.timer.currentTime < 45900)
		headline = STRING_SHARED_SKILL_ISSUE
	else if (bluespheres.timer.currentTime >= 50000)
		headline = STRING_SHARED_LESSON_IN_SPEED
	
	if (SETTING_BS_MENU_OPTIMIZATION == 0x02)
	{
		BlueSpheresTimer.TextManager.drawText(getScreenWidth() / 0x02, 25, 0xf000, 0x01, headline, 0x00)
		return
	}
	
	for (u8 A = 0x00; A < 0x04; ++A)
		BlueSpheresTimer.TextManager.drawText(px + (offset * A) - ((level.framecounter * 0x02) % offset), 25, 0xf000, 0x01, headline, 0x00)
}

function void BlueSpheresTimer.Menu.shared.items()
{
	s32 px = getScreenWidth() / 0x02
	s32 py = 46
	
	BlueSpheresTimer.Menu.lobby.items.keys(px, py)
	BlueSpheresTimer.Menu.shared.items.menu(px, py)
	
	BlueSpheresTimer.Menu.shared.items.betstime(px, py)
	BlueSpheresTimer.Menu.shared.items.currenttime(px, py)
	
	BlueSpheresTimer.Menu.timeattack.items.playerselect(px, py)
	BlueSpheresTimer.Menu.timeattack.items.levelselect(px, py)
}

function void BlueSpheresTimer.Menu.shared.items.menu(s32 px, s32 py)
{
	BlueSpheresTimer.TextManager.drawTextMenuItem(px, getScreenHeight() - py - 16, 0xf000, 0x01, STRING_RETRY, 0x01, bluespheres.shared.buttonselected, 0x00)
	
	BlueSpheresTimer.TextManager.drawTextMenuItem(px, getScreenHeight() - py, 0xf000, 0x01, STRING_BACK, 0x01, bluespheres.shared.buttonselected, 0x01)
}

function void BlueSpheresTimer.Menu.shared.items.betstime(s32 px, s32 py)
{
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, STRING_SHARED_BEST_TIME, 0x01)
	
	py += 16
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, stringformat("* %s *", BlueSpheresTimer.getFormatTime(BlueSpheresTimer.Records.getRecordTime(0x01))), 0x02)
}

function void BlueSpheresTimer.Menu.shared.items.currenttime(s32 px, s32 py)
{
	py += 44
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, STRING_SHARED_YOUR_TIME, 0x01)
	
	py += 16
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, stringformat("* %s *", BlueSpheresTimer.getFormatTime(bluespheres.timer.currentTime)), 0x02)
}

function void BlueSpheresTimer.Menu.shared.updateControl()
{
	// Ignore if popup menu active
	if (bluespheres.popup.show)
		return
	
	// Button selection
	if (control.pad1.pressed & (CONTROL_UP | CONTROL_DOWN))
	{
		playSound(0x5b)
		if (control.pad1.pressed & CONTROL_UP)
		{
			if (bluespheres.shared.buttonselected != 0x00)
				--bluespheres.shared.buttonselected
			else
				bluespheres.shared.buttonselected = bluespheres.buttons.setup
		}
		else if (control.pad1.pressed & CONTROL_DOWN)
		{
			if (bluespheres.shared.buttonselected < bluespheres.buttons.setup)
				++bluespheres.shared.buttonselected
			else
				bluespheres.shared.buttonselected = 0x00
		}
	}
	
	// Start the selected stage
	if (control.pad1.pressed & CONTROL_START)
	{
		if (bluespheres.shared.buttonselected == 0x00)
		{
			playSound(0xaf)
			bluespheres.timeattack.active = 0x01
			BlueSpheresTimer.EntryFunctions.startSpecialStage()
		}
		if (bluespheres.shared.buttonselected == 0x01)
		{
			playSound(0x63)
			bluespheres.timeattack.active = 0x00
			global.game_mode = 0x85
		}
	}
	
	// Go back to the time attack menu
	if (control.pad1.pressed & CONTROL_B)
	{
		playSound(0xad)
		bluespheres.timeattack.active = 0x00
		global.game_mode = 0x85
	}
}