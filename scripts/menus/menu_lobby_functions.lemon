function void BlueSpheresTimer.Menu.lobby.headline()
{
	s32 offset = 230
	s32 px = getScreenWidth() / 0x02 - offset
	
	for (u8 A = 0x00; A < 0x04; ++A)
		BlueSpheresTimer.TextManager.drawText(px + (offset * A) - ((level.framecounter * 0x02) % offset), 25, 0xf000, 0x01, STRING_LOBBY_HEADLINE, 0x00)
}

function void BlueSpheresTimer.Menu.lobby.items()
{
	s32 px = getScreenWidth() / 0x02
	s32 py = getScreenHeight() / 0x02
	
	BlueSpheresTimer.Menu.lobby.items.keys(px, py)
	BlueSpheresTimer.Menu.lobby.items.menu(px, py)
}

function void BlueSpheresTimer.Menu.lobby.items.keys(s32 px, s32 py)
{
	// Ignore if popup menu active
	if (bluespheres.popup.show)
		return
	
	// Set position
	px = 18
	py = getScreenHeight() - 18
	
	BlueSpheresTimer.TextManager.drawKeyboard(px, py, 0xf000, "b")
	px += 13
	BlueSpheresTimer.TextManager.drawKeyboard(px, py, 0xf000, "start")
	px += 18 // Additional offset by "start"
	px += 13
	BlueSpheresTimer.TextManager.drawKeyboard_dual(px, py, 0xf000, "up", "down", 0x00)
}

function void BlueSpheresTimer.Menu.lobby.items.menu(s32 px, s32 py)
{
	py -= 9
	string button_0x00 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.lobby.buttonselected, 0x00, STRING_LOBBY_TIME_ATTACK)
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, button_0x00, 0x01)
	
	py += 18
	string button_0x01 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.lobby.buttonselected, 0x01, STRING_LOBBY_GET_BLUE_SPHERES)
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, button_0x01, 0x01)
}

function void BlueSpheresTimer.Menu.lobby.updateControl()
{
	// Ignore if popup menu active
	if (bluespheres.popup.show)
		return
	
	// Button selection
	if (control.pad1.pressed & (CONTROL_UP | CONTROL_DOWN))
	{
		playSound(0x5b)
		if (control.pad1.pressed & CONTROL_UP)
		{
			if (bluespheres.lobby.buttonselected != 0x00)
				--bluespheres.lobby.buttonselected
			else
				bluespheres.lobby.buttonselected = bluespheres.buttons.setup
		}
		else if (control.pad1.pressed & CONTROL_DOWN)
		{
			if (bluespheres.lobby.buttonselected < bluespheres.buttons.setup)
				++bluespheres.lobby.buttonselected
			else
				bluespheres.lobby.buttonselected = 0x00
		}
	}
	
	// Go to the selected game
	if (control.pad1.pressed & CONTROL_START)
	{
		playSound(0x63)
		if (bluespheres.lobby.buttonselected == 0x00)
		{
			bluespheres.timeattack.buttonselected = 0x01
			bluespheres.minigame_mode = 0x00
			bluespheres.timeattack.active = 0x00
			global.game_mode = 0x85
		}
		if (bluespheres.lobby.buttonselected == 0x01)
		{
			bluespheres.minigame_mode = 0x01
			FadeOutScreenBlocking()
			
			levelselect.characters = CHARS_SONIC_ALONE
			u8[0xffffffa1] = 0x00
			fn04ceb6()
			
			VDP.Config.setActiveDisplay(false)
			global.game_mode = 0x2c
		}
	}
	
	// Go back to the main menu
	if (control.pad1.pressed & CONTROL_B)
	{
		playSound(0xad)
		Game.returnToMainMenu()
	}
}