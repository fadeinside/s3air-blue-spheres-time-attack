function void BlueSpheresTimer.Menu.options.setup()
{
	// Screen fade
	FadeOutScreenBlocking()
	
	// Global variables setup
	bluespheres.buttons.setup = 0x02
	bluespheres.options.buttonselected = bluespheres.buttons.setup
	
	// Menu variables
	s32 menu.headlinecounter
	
	// VRAM stuff
	waitForNextFrame()
	fn0011ca()
	fn04c8e4()
	
	// Main loop
	while (true)
	{
		FadeInFromWhite()
		waitForNextFrame()
		
		// Button selection
		if (!bluespheres.popup.show && (control.pad1.pressed & (CONTROL_UP | CONTROL_DOWN)))
		{
			playSound(0x5b)
			if (control.pad1.pressed & CONTROL_UP)
			{
				if (bluespheres.options.buttonselected != 0x00)
					--bluespheres.options.buttonselected
				else
					bluespheres.options.buttonselected = bluespheres.buttons.setup
			}
			else if (control.pad1.pressed & CONTROL_DOWN)
			{
				if (bluespheres.options.buttonselected < bluespheres.buttons.setup)
					++bluespheres.options.buttonselected
				else
					bluespheres.options.buttonselected = 0x00
			}
		}
		
		// Let's make the headline run across the screen from right to left
		++menu.headlinecounter
		if (menu.headlinecounter > 185)
			menu.headlinecounter = 0x00
			
		// Perform the action of selected button when start pressed
		if (!bluespheres.popup.show && (control.pad1.pressed == CONTROL_START))
		{
			if (bluespheres.options.buttonselected == 0x00)
			{
				if (BlueSpheresTimer.Records.writed() > 0x00)
				{
					playSound(0x63)
					bluespheres.popup.show = 0x01
					waitForNextFrame()
				}
				else
				{
					playSound(0x7b)
				}
			}
			if (bluespheres.options.buttonselected == 0x01)
			{
				if (BlueSpheresTimer.Achievements.writed() > 0x00)
				{
					playSound(0x63)
					bluespheres.popup.show = 0x01
					waitForNextFrame()
				}
				else
				{
					playSound(0x7b)
				}
			}
			if (bluespheres.options.buttonselected == bluespheres.buttons.setup)
			{
				playSound(0x63)
				global.game_mode = 0x85
			}
		}
		
		// End the current loop if the mode don't match
		if (global.game_mode != 0x86)
			break
		
		// Checking the activity of achievements
		BlueSpheresTimer.Achievements.setup()
		
		// Screen items
		BlueSpheresTimer.Menu.options.popups()
		BlueSpheresTimer.Menu.options.headline(menu.headlinecounter)
		BlueSpheresTimer.Menu.options.items()
		
		BlueSpheresTimer.Menu.timeattack.background()
		
		// Go back to the time attack menu
		if (!bluespheres.popup.show && (control.pad1.pressed & CONTROL_B))
		{
			playSound(0xad)
			global.game_mode = 0x85
		}
		
		// Clear sprites every frame
		Renderer.resetSprites()
	}
}