function void BlueSpheresTimer.Menu.timeattack.popups()
{
	if (bluespheres.popup.show == 0x00)
		return
		
	u64 accept_text = "Okay"
	
	if (BlueSpheresTimer.Emeralds.allEqual(0x01) && !BlueSpheresTimer.Data.getValue(bluespheres.stage8Unlocked))
	{
		BlueSpheresTimer.Menu.popup.show("now unlocked!", "Secret 8 stage", "", "", accept_text)
		if (control.pad1.pressed & CONTROL_START)
		{
			playSound(0x63)
			BlueSpheresTimer.Data.setValue(bluespheres.stage8Unlocked, 0x01)
			
			bluespheres.popup.show = 0x00
		}
	}
	if (BlueSpheresTimer.Emeralds.allEqual(0x01) && BlueSpheresTimer.Data.getValue(bluespheres.stage8Unlocked) && !BlueSpheresTimer.Data.getValue(bluespheres.skUnlocked))
	{
		BlueSpheresTimer.Menu.popup.show("now unlocked!", "`Sonic and Knuckles` stages", "", "", accept_text)
		if (control.pad1.pressed & CONTROL_START)
		{
			playSound(0x63)
			BlueSpheresTimer.Data.setValue(bluespheres.skUnlocked, 0x01)
			BlueSpheresTimer.Emeralds.replaceAll(0x02)
			
			bluespheres.popup.show = 0x00
		}
	}
}

function void BlueSpheresTimer.Menu.timeattack.background()
{
	// SK Blue Sphere logo
	Renderer.drawCustomSprite("sklogo", getScreenWidth() / 0x02, 40, 0x40, 0x00, 0xF000)
	
	// Copyright
	Renderer.drawCustomSprite("icon_copyright", getScreenWidth() - 104, 210, 0x00, 0x40, 0xf000)
	Renderer.drawCustomSprite("title_screen_cr", getScreenWidth() - 92, 206, 0x40, 0x00, 0xf000)
	
	// Draw a gradient so that the objects are more clearly visible on the background
	for (u8 A = 0x00; A <= 0x01 + (getScreenWidth() / 0x10); ++A)
		Renderer.drawCustomSprite("background_menu", 0x10 * A, 0x00, 0x00, 0x40, 0xf000)
	
	// Render stage preview
	BlueSpheresTimer.Menu.timeattack.stagePreview()
}

function void BlueSpheresTimer.Menu.timeattack.stagePreview()
{
	// We will make the ground static, as in the first release, when the menu optimization setting is active
	if (SETTING_BS_MENU_OPTIMIZATION)
	{
		bluespheres.position.x = 1024
		bluespheres.position.y = 1024
		bluespheres.direction = 96
		
		// Load ground colors
		A1 = u32[0x0088ba + bluespheres.timeattack.levelselected * 0x10]
		if (bluespheres.timeattack.layoutselected)
			A1 += 0x0130
		
		if (Mods.isModActive("Sonic 3: D.A. Garden Edition"))
			A1 += 0x2cb80e
		
		u32[0xffffe446] = A1
		A2 = 0xfffffc80
		
		copyMemory(A2 + 0x70, A1, 0x10)
	
		// Render ground
		BlueSpheres.renderCustomGround()
		
		return
	}
	
	// Some things to make the ground animate
	u8[0xffffe433] = 0x00
	bluespheres.lifted_height = 0x00
	bluespheres.movement_speed = 0x500
	
	D2.s16 = bluespheres.movement_speed
	D0.u8 = bluespheres.direction
	
	LookupSinCos()
	D0.s32 = s32(D0.s16) * D2.s16
	D1.s32 = s32(D1.s16) * D2.s16
	bluespheres.position.x -= D0 >> 0x10
	bluespheres.position.y -= D1 >> 0x10
	
	// Load ground colors
	A1 = u32[0x0088ba + bluespheres.timeattack.levelselected * 0x10]
	if (bluespheres.timeattack.layoutselected)
		A1 += 0x0130
	
	if (Mods.isModActive("Sonic 3: D.A. Garden Edition"))
		A1 += 0x2cb80e
	
	u32[0xffffe446] = A1
	A2 = 0xfffffc80
	
	copyMemory(A2 + 0x70, A1, 0x10)
	
	// Render ground
	BlueSpheres.renderCustomGround()
	
	// Ignore the spheres render if this setting is disabled
	if !(SETTING_BS_STAGE_PREVIEW_ENABLE)
	{
		bluespheres.direction = 0x40
		return
	}
	
	// Main game stages
	// "Sonic 3: D.A. Garden Edition" by Thorn_SRetro, check https://gamebanana.com/mods/151029
	if !(Mods.isModActive("Sonic 3: D.A. Garden Edition") && bluespheres.timeattack.characterselected == 0x02)
		Kosinski.decompress(u32[0x1e4078], 0xffff0000)
	else // Knuckles has a different level layout in DAGE
		Kosinski.decompress(0x2d3ad0, 0xffff0000)
	
	// Load level layout, either from S3 or S&K
	A2 = bluespheres.timeattack.layoutselected ? 0x008590 : 0x25e2d8
	A2 = u32[A2 + bluespheres.timeattack.levelselected * 0x04]
	if !(bluespheres.timeattack.layoutselected)
	{
		A2 += 0x200000
		if (Mods.isModActive("Sonic 3: D.A. Garden Edition") && bluespheres.timeattack.characterselected == 0x02) // Knuckles has a different level layout in DAGE
			A2 += 0x0679b0
	}
	
	zeroMemory(0xfffff000, 0x100)
	copyMemory(0xfffff100, A2, 0x400)
	zeroMemory(0xfffff500, 0x100)
	A2 += 0x400
	
	bool stageChanged = control.pad1.pressed & (CONTROL_UP | CONTROL_DOWN)
	bool layoutChanged = Input.buttonPressed(BUTTON_X) && BlueSpheresTimer.Data.getValue(bluespheres.skUnlocked)
	bool characterChanged = Input.buttonPressed(BUTTON_Y) && (bluespheres.timeattack.characterselected == 0x02 && Mods.isModActive("Sonic 3: D.A. Garden Edition")) // Knuckles has a different level layout in DAGE
	
	// Sets the camera in the right place
	if (global.game_mode == 0x85 && !bluespheres.popup.show && (stageChanged || layoutChanged || characterChanged))
	{
		u16[0xffffe426] = u16[A2]
		bluespheres.position.x = u16[A2+2]
		bluespheres.position.y = u16[A2+4]
	}
	
	if (bluespheres.direction != 0x00 || bluespheres.direction != 64 || bluespheres.direction != 128 || bluespheres.direction != 192) // Fixes camera direction when you return to the menu after red sphere or emerald
		u16[0xffffe426] = u16[A2]
		
	if (global.game_mode == 0x89 && bluespheres.position.x == 0x00) // Sets the correct camera position when you first log in to the Lobby
	{
		bluespheres.position.x = u16[A2+2]
		bluespheres.position.y = u16[A2+4]
	}
	
	fn00a0e6() // VRAM stuff
	fn009d9e() // Rings framerate
	
	// Render spheres
	BlueSpheres.RenderSpheres()
}

function void BlueSpheresTimer.Menu.timeattack.headline()
{
	s32 offset = 216
	s32 px = getScreenWidth() / 0x02 - offset
	
	for (u8 A = 0x00; A < 0x04; ++A)
		BlueSpheresTimer.TextManager.drawText(px + (offset * A) - ((level.framecounter * 0x02) % offset), 25, 0xf000, 0x01, "TIME ATTACK!", 0x00)
}

function void BlueSpheresTimer.Menu.timeattack.items()
{
	s32 px = getScreenWidth() / 0x02
	s32 py = 46
	
	BlueSpheresTimer.Menu.timeattack.items.menu(px, py)
	
	BlueSpheresTimer.Menu.timeattack.items.total(px, py)
	BlueSpheresTimer.Menu.timeattack.items.recordslist(px, py)
	
	BlueSpheresTimer.Menu.timeattack.items.playerselect(px, py)
	BlueSpheresTimer.Menu.timeattack.items.emeralds(px, py)
	
	BlueSpheresTimer.Menu.timeattack.items.levelselect(px, py)
	BlueSpheresTimer.Menu.timeattack.items.levelslayout(px, py)
}

function void BlueSpheresTimer.Menu.timeattack.items.menu(s32 px, s32 py)
{
	px -= 128
	BlueSpheresTimer.TextManager.drawKeyboard(px - 48, getScreenHeight() - py, 0xf000, "left")
	
	u64 button_0x00 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.timeattack.buttonselected, 0x00, "Options")
	BlueSpheresTimer.TextManager.drawText(px, getScreenHeight() - py, 0xf000, 0x01, button_0x00, 0x01)
	
	px += 128
	u64 button_0x01 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.timeattack.buttonselected, 0x01, "Start")
	BlueSpheresTimer.TextManager.drawText(px, getScreenHeight() - py, 0xf000, 0x01, button_0x01, 0x01)
	
	px += 128
	BlueSpheresTimer.TextManager.drawKeyboard(px + 64, getScreenHeight() - py, 0xf000, "right")
	
	u64 button_0x02 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.timeattack.buttonselected, 0x02, "Achievements")
	BlueSpheresTimer.TextManager.drawText(px, getScreenHeight() - py, 0xf000, 0x01, button_0x02, 0x01)
}

function void BlueSpheresTimer.Menu.timeattack.items.total(s32 px, s32 py)
{
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, "Total", 0x01)
	
	s32 minutes_total
	s32 seconds_total
	s32 centiseconds_total
	
	u8 gangMain = (bluespheres.timeattack.characterselected + 0x01)
	u8 gangExtra = bluespheres.timeattack.characterselected == 0x03 ? (bluespheres.timeattack.xtracharselected - 0x01) : 0x00
	
	// Number of stages (0x00 .. 0x06, 0x07)
	u8 maxStages = BlueSpheresTimer.Data.getValue(bluespheres.stage8Unlocked) ? 0x07 : 0x06
	for (u8 A = 0x00; A <= maxStages; ++A)
	{
		// Number of record places (0x01 .. 0x04)
		for (u8 B = 0x01; B <= 0x04; ++B)
		{
			u32 recordplace = 0x1024 * B
			u32 character = 0x128 * (gangMain + gangExtra)
			u32 stage = 0x16 * (A + 0x01)
			u32 layout = 0x08 * bluespheres.timeattack.layoutselected
			u32 mods = Mods.isModActive("Sonic 3: D.A. Garden Edition") ? 0x10000 : 0x00
			
			u32 recordOffset = bluespheres.recordDataAddress + recordplace + character + stage + layout + mods
			System.loadPersistentData(recordOffset, stringformat("SRAM_BlueSpheresTimer_Record_0x%06x", recordOffset), 0x04)
			
			// Get parsed data from address value
			s32 minutes = s32[recordOffset] / 10000
			s32 seconds = (s32[recordOffset] - (minutes * 10000)) / 100
			s32 centiseconds = (s32[recordOffset] - (minutes * 10000)) - (seconds * 100)
			
			//debugLog(stringformat("Loaded RAM 0x%06x: %s", recordOffset, BlueSpheresTimer.getFormatTime(minutes, seconds, centiseconds)))
			
			// If the address value is greater than 0x00, then add its parsed values to the variables. Otherwise add a static value to a variable
			if (s32[recordOffset] > 0x00)
			{
				minutes_total += minutes
				seconds_total += seconds
				centiseconds_total += centiseconds
			}
			else
			{
				minutes_total += 10
			}
		}
	}
	
	seconds_total += centiseconds_total / 60
	minutes_total += seconds_total / 60
	centiseconds_total %= 99
	seconds_total %= 60
	
	py += 16
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, stringformat("* %s *", BlueSpheresTimer.getFormatTime(minutes_total, seconds_total, centiseconds_total)), 0x02)
}

function void BlueSpheresTimer.Menu.timeattack.items.recordslist(s32 px, s32 py)
{
	py += 44
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, "Records", 0x01)
	
	for (u8 A = 0x01; A <= 0x04; ++A)
	{
		py += 16
		u64 name = (A == 0x01) ? "st" : ((A == 0x02) ? "nd" : ((A == 0x03) ? "rd" : ((A == 0x04) ? "th" : "")))
		BlueSpheresTimer.Menu.timeattack.items.table(px, py, 0xf000, 0x02, stringformat("%d%s", A, name), BlueSpheresTimer.Records.getRecordTime(A))
	}
}

function void BlueSpheresTimer.Menu.timeattack.items.playerselect(s32 px, s32 py)
{
	px -= 128
	if (global.game_mode != 0x87)
		BlueSpheresTimer.TextManager.drawKeyboard(px + 48, py, 0xf000, "y")
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, "Character", 0x01)
	BlueSpheresTimer.TextManager.drawText(px, py + 16, 0xf000, 0x01, "*    *", 0x02)
	
	u64 key
	if (bluespheres.timeattack.characterselected == 0x00)
		key = "continue_icon_sonic"
	else if (bluespheres.timeattack.characterselected == 0x01)
		key = "continue_icon_tails"
	else if (bluespheres.timeattack.characterselected == 0x02)
		key = "continue_icon_knuckles"
	else if (bluespheres.timeattack.characterselected == 0x03)
		key = stringformat("continue_icon_slot%d", bluespheres.timeattack.xtracharselected)
	
	if (Renderer.hasCustomSprite(key))
	{
		Renderer.drawCustomSprite(key, px, py + 28, 0x40, 0x00, 0xf000)
	}
}

function void BlueSpheresTimer.Menu.timeattack.items.emeralds(s32 px, s32 py)
{
	px -= 128
	py += 44
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, "Emeralds", 0x01)
	
	u8 width = BlueSpheresTimer.Data.getValue(bluespheres.skUnlocked) ? 16 : 14
	
	px -= BlueSpheresTimer.Data.getValue(bluespheres.skUnlocked) ? 26 : 22
	py += 16
	for (u8 A = 0x00; A < 0x04; ++A)
	{
		Renderer.drawCustomSprite(BlueSpheresTimer.Emeralds.getEmerald(A), px + (width * A), py, 0x00, 0x40, 0xf000)
		//debugLog(stringformat("Loaded RAM 0x%06x: 0x%02x", BlueSpheresTimer.Emeralds.getAddress(A), BlueSpheresTimer.Emeralds.getStatus(A)))
	}
	
	if !(BlueSpheresTimer.Data.getValue(bluespheres.stage8Unlocked))
		px += 7
	
	py += 15
	for (u8 B = 0x00; B < (BlueSpheresTimer.Data.getValue(bluespheres.stage8Unlocked) ? 0x04 : 0x03); ++B)
	{
		Renderer.drawCustomSprite(BlueSpheresTimer.Emeralds.getEmerald(B + 0x04), px + (width * B), py, 0x00, 0x40, 0xf000)
		//debugLog(stringformat("Loaded RAM 0x%06x: 0x%02x", BlueSpheresTimer.Emeralds.getAddress(B + 0x04), BlueSpheresTimer.Emeralds.getStatus(B + 0x04)))
	}
}

function void BlueSpheresTimer.Menu.timeattack.items.levelselect(s32 px, s32 py)
{
	px += 128
	if (global.game_mode != 0x87)
		BlueSpheresTimer.TextManager.drawKeyboard_dual(px + 32, py, 0xf000, "up", "down", 0x00)
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, "Level", 0x01)
	
	py += 16
	if (bluespheres.timeattack.levelselected == 0x07)
		BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, "* Secret *", 0x01)
	else
		BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, stringformat("* 0%d *", bluespheres.timeattack.levelselected + 0x01), 0x02)
}

function void BlueSpheresTimer.Menu.timeattack.items.levelslayout(s32 px, s32 py)
{
	px += 128
	u8 alpha = BlueSpheresTimer.Data.getValue(bluespheres.skUnlocked) ? 0xff : 0x90
	
	py +=  44
	BlueSpheresTimer.TextManager.drawKeyboard(px + 64, py, 0xf000, "a")
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, "Levels layout", 0x01, alpha)
	
	py += 16
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, stringformat("* %s *", bluespheres.timeattack.layoutselected ? "SK" : "S3"), 0x01, alpha)
}

function void BlueSpheresTimer.Menu.timeattack.items.table(s32 px, s32 py, u16 renderQueue, u8 align, u64 text, s32 input_data)
{
	px -= 12
	BlueSpheresTimer.TextManager.drawText(px - 38, py, renderQueue, align, text, 0x01)
	
	if (input_data == 0x00)
	{
		BlueSpheresTimer.TextManager.drawText(px, py, renderQueue, align, "--'--`--", 0x02)
		return
	}
	
	BlueSpheresTimer.TextManager.drawText(px, py, renderQueue, align, BlueSpheresTimer.getFormatTime(input_data), 0x02)
}