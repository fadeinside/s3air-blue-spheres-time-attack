function void BlueSpheresTimer.Menu.lobby.setup()
{
	// Global variables setup
	level.framecounter = 0x00
	bluespheres.achievements.extraselected = 0x00
	bluespheres.buttons.setup = Mods.isModActive("Blue Sphere Plus") ? 0x02 : 0x01
	if (SETTING_BS_DEFAULT_CHARACTER != 0x00 && bluespheres.character_defined != 0x01)
	{
		bluespheres.timeattack.characterselected = SETTING_BS_DEFAULT_CHARACTER
		bluespheres.character_defined = 0x01
	}
	
	// "Blue Sphere Plus" by Veenee, check https://gamebanana.com/mods/297356
	// Load additional data from Shared Memory for work with BS Plus
	if (Mods.isModActive("Blue Sphere Plus"))
		System.loadPersistentData(0x801100, "SRAM_BlueSphereProgressExt", 0x02)
	
	// Play Blue Sphere game music
	playMusic(0x28)
	
	// Screen fades
	FadeOutScreenBlocking()
	
	// VRAM stuff
	BlueSpheresTimer.VRAMClear()
	BlueSpheresTimer.VRAMLoad_stagePreview()
	
	// Main loop
	while (true)
	{
		global.frame_state = 0x12
		waitForNextFrame()
		
		// Start counting arrival frames in the menu
		++level.framecounter
		
		// End the current loop if the mode don't match
		if (global.game_mode != 0x89)
			break
		
		// Control update
		BlueSpheresTimer.Menu.lobby.updateControl()
		
		// Achievements update
		BlueSpheresTimer.Achievements.update()
		
		// Screen items
		BlueSpheresTimer.Menu.lobby.headline()
		BlueSpheresTimer.Menu.lobby.items()
		
		// Background update
		BlueSpheresTimer.BackgroundManager.update()
		
		// Screen fades
		BlueSpheresTimer.FadeInFromWhite()
		
		// Clear sprites every frame
		RenderSprites()
	}
}