function void BlueSpheresTimer.Menu.lobby.setup()
{
	// Global variables setup
	level.framecounter = 0x00
	BlueSpheresTimer.TooltipManager.resetTooltip()
	
	bluespheres.minigame_mode = 0x00
	bluespheres.greenspheres.active = 0x00
	
	bluespheres.achievements.extraselected = 0x00
	bluespheres.buttons.setup = 0x03
	if (Mods.isModActive("Blue Sphere Plus"))
		bluespheres.buttons.setup += 0x01
	if (Mods.isModActive("Sonic 3: D.A. Garden Edition"))
		bluespheres.buttons.setup += 0x01
	
	if (bluespheres.lobby.buttonselected == 0x00)
		bluespheres.lobby.buttonselected = 0x02
	
	if (SETTING_BS_DEFAULT_CHARACTER != 0x00 && bluespheres.character_defined != 0x01)
	{
		bluespheres.timeattack.characterselected = SETTING_BS_DEFAULT_CHARACTER
		bluespheres.character_defined = 0x01
	}
	bluespheres.backToLobby = 0x00
	
	// "Blue Sphere Plus" by Veenee, check https://gamebanana.com/mods/297356
	// Load additional data from Shared Memory for work with BS Plus
	if (Mods.isModActive("Blue Sphere Plus"))
		System.loadPersistentData(0x801100, "SRAM_BlueSphereProgressExt", 0x02)
	zeroMemory(0xffffffa6, 0x04)
	
	// Play Blue Sphere game music
	playMusic(0x28)
	
	// Screen fades
	FadeOutScreenBlocking()
	
	// VRAM stuff
	BlueSpheresTimer.VRAMClear()
	BlueSpheresTimer.VRAMLoad_stagePreview()
	
	// Main loop
	while (true)
	{
		global.frame_state = 0x12
		BlueSpheresTimer.waitForNextFrame()
		
		// Start counting arrival frames in the menu
		++level.framecounter
		
		// End the current loop if the mode don't match
		if (global.game_mode != 0x89)
			break
		
		// Control update
		BlueSpheresTimer.Menu.lobby.updateControl()
		
		// Tooltip update
		BlueSpheresTimer.TooltipManager.update()
		
		// Achievements update
		BlueSpheresTimer.Achievements.update()
		
		// Screen items
		BlueSpheresTimer.Menu.lobby.headline()
		BlueSpheresTimer.Menu.lobby.items()
		
		// Background update
		BlueSpheresTimer.BackgroundManager.update()
		
		// Screen fades
		BlueSpheresTimer.FadeInFromWhite()
		
		// Clear sprites every frame
		RenderSprites()
	}
}