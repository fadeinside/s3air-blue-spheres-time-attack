function void BlueSpheresTimer.Menu.lobby.setup()
{
	// Screen fades
	FadeOutScreenBlocking()
	
	// Global variables setup
	bluespheres.buttons.setup = Mods.isModActive("Blue Sphere Plus") ? 0x02 : 0x01
	
	// Menu variables
	s32 menu.headlinecounter
	
	// Play Blue Sphere game music
	playMusic(0x28)
	
	// VRAM stuff
	waitForNextFrame()
	fn0011ca()
	fn04c8e4()
	
	// Main loop
	while (true)
	{
		FadeInFromWhite()
		waitForNextFrame()
		
		// Button selection
		if (control.pad1.pressed & (CONTROL_UP | CONTROL_DOWN))
		{
			playSound(0x5b)
			if (control.pad1.pressed & CONTROL_UP)
			{
				if (bluespheres.lobby.buttonselected != 0x00)
					--bluespheres.lobby.buttonselected
				else
					bluespheres.lobby.buttonselected = bluespheres.buttons.setup
			}
			else if (control.pad1.pressed & CONTROL_DOWN)
			{
				if (bluespheres.lobby.buttonselected < bluespheres.buttons.setup)
					++bluespheres.lobby.buttonselected
				else
					bluespheres.lobby.buttonselected = 0x00
			}
		}
		
		// Let's make the headline run across the screen from right to left
		++menu.headlinecounter
		if (menu.headlinecounter > 230)
			menu.headlinecounter = 0x00
		
		// Go to the selected game
		if (control.pad1.pressed & CONTROL_START)
		{
			if (bluespheres.lobby.buttonselected == 0x00)
			{
				bluespheres.timeattack.buttonselected = 0x01
				global.game_mode = 0x85
				break
			}
			if (bluespheres.lobby.buttonselected == 0x01)
			{
				FadeOutScreenBlocking()
				
				u8[0xffffffa1] = 0x01
				bluespheregame.menu_usemaingamestages = false
				
				global.game_mode = 0x2c
				break
			}
			if (bluespheres.lobby.buttonselected == 0x02)
			{
				FadeOutScreenBlocking()
				
				u8[0xffffffa1] = 0x01
				bluespheregame.menu_usemaingamestages = true
				bluespheregame.menu_selection = 0x02
				
				global.game_mode = 0x2c
				break
			}
		}
		
		// Checking the activity of achievements
		BlueSpheresTimer.Achievements.setup()
		
		// Screen items
		BlueSpheresTimer.Menu.lobby.headline(menu.headlinecounter)
		BlueSpheresTimer.Menu.lobby.items()
		BlueSpheresTimer.Menu.timeattack.background()
		
		// Go back to the main menu
		if (control.pad1.pressed & CONTROL_B)
		{
			playSound(0xad)
			FadeOutScreenBlocking()
			
			Game.returnToMainMenu()
		}
		
		// Clear sprites every frame
		Renderer.resetSprites()
	}
}