function void BlueSpheresTimer.Menu.popup.background()
{
	// Rendering the background here no longer makes sense if this setting is active
	if (SETTING_BS_MENU_OPTIMIZATION)
		return
	
	for (u8 A = 0x00; A <= 0x01 + (getScreenWidth() / 0x10); ++A)
		Renderer.drawCustomSprite("background_menu", 0x10 * A, 0x00, 0x00, 0x40, 0xf000, 0x00, (global.game_mode == 0x88) ? 0xdb : 0xca)
}

function void BlueSpheresTimer.Menu.popup.items.description(s32 px, s32 py, string text1, string text2, string text3, string text4)
{
	BlueSpheresTimer.TextManager.drawTextWithLine(px, py, 0xf000, 0x01, "", 0x01, 0x90)
	
	if (text1 != "")
	{
		py -= 16
		BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, text1, 0x01)
	}
	if (text2 != "")
	{
		py -= 16
		BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, text2, 0x01)
	}
	if (text3 != "")
	{
		py -= 16
		BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, text3, 0x01)
	}
	if (text4 != "")
	{
		py -= 16
		BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, text4, 0x01)
	}
	if (global.game_mode == 0x88 && !(BlueSpheresTimer.Achievements.getTitle(bluespheres.achievements.buttonselected) == STRING_ACHIEVEMENTS_MOD_REQUIRE))
	{
		py -= 16
		BlueSpheresTimer.TextManager.drawTextWithLine(px, py, 0xf000, 0x01, "", 0x01, 0x90)
	}
}

// - - - - - - - - - - - - - - - - - - - //
// Popup (1 button) 				//
// - - - - - - - - - - - - - - - - - - - //

function void BlueSpheresTimer.Menu.popup.show(string text1, string text2, string text3, string text4, string button)
{
	bluespheres.popup.buttonselected = 0x00
	
	BlueSpheresTimer.Menu.popup.items(text1, text2, text3, text4, button)
	BlueSpheresTimer.Menu.popup.background()
}

function void BlueSpheresTimer.Menu.popup.items(string text1, string text2, string text3, string text4, string button)
{
	s32 px = getScreenWidth() / 0x02
	s32 py = getScreenHeight() / 0x02
	
	if (text2 != "")
		py += 0x08
	if (text3 != "")
		py += 0x08
	if (text4 != "")
		py += 0x08
		
	if (global.game_mode == 0x88 && !(BlueSpheresTimer.Achievements.getTitle(bluespheres.achievements.buttonselected) == STRING_ACHIEVEMENTS_MOD_REQUIRE))
	{
		bluespheres.popup.offsetDescription = py
		py += 16
	}
	
	BlueSpheresTimer.Menu.popup.items.keys1(px, py)
	BlueSpheresTimer.Menu.popup.items.menu(px, py, button)
	
	BlueSpheresTimer.Menu.popup.items.description(px, py, text1, text2, text3, text4)
}

function void BlueSpheresTimer.Menu.popup.items.keys1(s32 px, s32 py)
{
	// Set position
	px = 18
	py = getScreenHeight() - 18
	
	BlueSpheresTimer.TextManager.drawKeyboard(px, py, 0xf000, "start")
}

function void BlueSpheresTimer.Menu.popup.items.menu(s32 px, s32 py, string button)
{
	py += 16
	string button_0x00 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.popup.buttonselected, 0x00, button)
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, button_0x00, 0x01)
}

// - - - - - - - - - - - - - - - - - - - //
// Popup (2 buttons)				//
// - - - - - - - - - - - - - - - - - - - //

function void BlueSpheresTimer.Menu.popup.show(string text1, string text2, string text3, string text4, string button1, string button2)
{
	// Button selection
	if (control.pad1.pressed & (CONTROL_LEFT | CONTROL_RIGHT))
	{
		playSound(0x5b)
		if (control.pad1.pressed & CONTROL_LEFT)
		{
			if (bluespheres.popup.buttonselected != 0x00)
				--bluespheres.popup.buttonselected
			else
				bluespheres.popup.buttonselected = 0x01
		}
		else if (control.pad1.pressed & CONTROL_RIGHT)
		{
			if (bluespheres.popup.buttonselected < 0x01)
				++bluespheres.popup.buttonselected
			else
				bluespheres.popup.buttonselected = 0x00
		}
	}
			
	BlueSpheresTimer.Menu.popup.items(text1, text2, text3, text4, button1, button2)
	BlueSpheresTimer.Menu.popup.background()
}

function void BlueSpheresTimer.Menu.popup.items(string text1, string text2, string text3, string text4, string button1, string button2)
{
	s32 px = getScreenWidth() / 0x02
	s32 py = getScreenHeight() / 0x02
	
	if (global.game_mode == 0x88 && !(BlueSpheresTimer.Achievements.getTitle(bluespheres.achievements.buttonselected) == STRING_ACHIEVEMENTS_MOD_REQUIRE))
		py += 16
	
	if (text2 != "")
		py += 0x08
	if (text3 != "")
		py += 0x08
	if (text4 != "")
		py += 0x08
	
	BlueSpheresTimer.Menu.popup.items.keys2(px, py)
	BlueSpheresTimer.Menu.popup.items.menu(px, py, button1, button2)
	
	BlueSpheresTimer.Menu.popup.items.description(px, py, text1, text2, text3, text4)
}

function void BlueSpheresTimer.Menu.popup.items.keys2(s32 px, s32 py)
{
	// Set position
	px = 18
	py = getScreenHeight() - 18
	
	BlueSpheresTimer.TextManager.drawKeyboard(px, py, 0xf000, "start")
	px += 18 // Additional offset by "start"
	px += 13
	BlueSpheresTimer.TextManager.drawKeyboard_dual(px, py, 0xf000, "left", "right", 0x00)
}

function void BlueSpheresTimer.Menu.popup.items.menu(s32 px, s32 py, string button1, string button2)
{
	py += 16
	
	px -= 64
	string button_0x00 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.popup.buttonselected, 0x00, button1)
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, button_0x00, 0x01)
	
	px += 128
	string button_0x01 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.popup.buttonselected, 0x01, button2)
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, button_0x01, 0x01)
}