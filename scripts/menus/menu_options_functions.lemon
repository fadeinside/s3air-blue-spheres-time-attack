function void BlueSpheresTimer.Menu.options.popups()
{
	if (bluespheres.popup.show == 0x00)
		return
		
	u64 accept_text = "Reset"
	u64 deny_text = "Cancel"
	
	// A reset is needed here, because if there is no reset, then the saved values in the variables will activate BlueSpheresTimer.Achievements.setup() again
	bluespheres.tempValue1 = 0x00
	bluespheres.blue_collected = 0x00
	BlueSpheresTimer.resetTimer()
	
	if (bluespheres.options.buttonselected == 0x00)
	{
		BlueSpheresTimer.Menu.popup.show("You are sure?", "This action cannot be undone", "your saved data", "This will completely erase all", deny_text, accept_text)
		if (control.pad1.pressed & CONTROL_START)
		{
			playSound(0x63)
			if (bluespheres.popup.buttonselected == 0x00)
				bluespheres.popup.show = 0x00
			
			if (bluespheres.popup.buttonselected == 0x01)
			{
				playSound(0x68)
				BlueSpheresTimer.Records.reset()
				BlueSpheresTimer.Emeralds.reset()
				BlueSpheresTimer.Achievements.reset()
				
				BlueSpheresTimer.Data.reset(bluespheres.stage8Unlocked)
				BlueSpheresTimer.Data.reset(bluespheres.skUnlocked)
				
				// Override selected level and layout variables
				if (bluespheres.timeattack.levelselected == 0x07)
					bluespheres.timeattack.levelselected = 0x06
				if (bluespheres.timeattack.layoutselected)
					bluespheres.timeattack.layoutselected = 0x00
				
				bluespheres.popup.show = 0x00
				bluespheres.popup.buttonselected = 0x00
			}
		}
	}
	else if (bluespheres.options.buttonselected == 0x01)
	{
		BlueSpheresTimer.Menu.popup.show("You are sure?", "This action cannot be undone", "", "", deny_text, accept_text)
		if (control.pad1.pressed & CONTROL_START)
		{
			playSound(0x63)
			if (bluespheres.popup.buttonselected == 0x00)
				bluespheres.popup.show = 0x00
			
			if (bluespheres.popup.buttonselected == 0x01)
			{
				playSound(0x68)
				BlueSpheresTimer.Records.reset()
				
				bluespheres.popup.show = 0x00
				bluespheres.popup.buttonselected = 0x00
			}
		}
	}
	else if (bluespheres.options.buttonselected == 0x02)
	{
		BlueSpheresTimer.Menu.popup.show("You are sure?", "This action cannot be undone", "The emeralds will also be reset", "", deny_text, accept_text)
		if (control.pad1.pressed & CONTROL_START)
		{
			playSound(0x63)
			if (bluespheres.popup.buttonselected == 0x00)
				bluespheres.popup.show = 0x00
			
			if (bluespheres.popup.buttonselected == 0x01)
			{
				playSound(0x68)
				BlueSpheresTimer.Achievements.reset()
				
				// This check, actually, allows me not to fix some bugs related to get emeralds at the stage
				if (BlueSpheresTimer.Data.getValue(bluespheres.skUnlocked))
				{
					BlueSpheresTimer.Emeralds.replaceAll(0x02)
					BlueSpheresTimer.Achievements.setValue(ACHIEVEMENT_BS_ALL_CHAOS_EMERALDS, 0xfa2)
				}
				else
				{
					BlueSpheresTimer.Emeralds.replaceAll(0x00)
				}
				
				bluespheres.popup.show = 0x00
				bluespheres.popup.buttonselected = 0x00
			}
		}
	}
}

function void BlueSpheresTimer.Menu.options.headline()
{
	s32 offset = 150
	s32 px = getScreenWidth() / 0x02 - offset
	
	for (u8 A = 0x00; A < 0x04; ++A)
		BlueSpheresTimer.TextManager.drawText(px + (offset * A) - ((level.framecounter * 0x02) % offset), 25, 0xf000, 0x01, "OPTIONS!", 0x00)
}

function void BlueSpheresTimer.Menu.options.items()
{
	s32 px = getScreenWidth() / 0x02
	s32 py = 46
	
	BlueSpheresTimer.Menu.options.items.keys(px, py)
	BlueSpheresTimer.Menu.options.items.menu(px, py)
	
	BlueSpheresTimer.Menu.options.items.list(px, py)
}

function void BlueSpheresTimer.Menu.options.items.keys(s32 px, s32 py)
{
	// Ignore if popup menu active
	if (bluespheres.popup.show)
		return
	
	// Set position
	px = 18
	py = getScreenHeight() - 18
	
	// Bools 
	bool hasSavedData = bluespheres.options.buttonselected == 0x00 && (BlueSpheresTimer.Records.writed() > 0x00 || BlueSpheresTimer.Emeralds.writed() > 0x00 || BlueSpheresTimer.Achievements.writed() > 0x00)
	bool hasSavedRecords = bluespheres.options.buttonselected == 0x01 && BlueSpheresTimer.Records.writed() > 0x00
	bool hasSavedAchievements = bluespheres.options.buttonselected == 0x02 && BlueSpheresTimer.Achievements.writed() > 0x00
	bool isBackButton = bluespheres.options.buttonselected == bluespheres.buttons.setup
	bool showStartButton = hasSavedData || hasSavedRecords || hasSavedAchievements || isBackButton
	
	BlueSpheresTimer.TextManager.drawKeyboard(px, py, 0xf000, "b")
	px += 13
	if (showStartButton)
	{
		BlueSpheresTimer.TextManager.drawKeyboard(px, py, 0xf000, "start")
		px += 18 // Additional offset by "start"
		px += 13
	}
	BlueSpheresTimer.TextManager.drawKeyboard_dual(px, py, 0xf000, "up", "down", 0x00)
}

function void BlueSpheresTimer.Menu.options.items.menu(s32 px, s32 py)
{
	u64 button_0xff = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.options.buttonselected, bluespheres.buttons.setup, "Back")
	BlueSpheresTimer.TextManager.drawText(px, getScreenHeight() - py, 0xf000, 0x01, button_0xff, 0x01)
}

function void BlueSpheresTimer.Menu.options.items.list(s32 px, s32 py)
{
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, "Erase", 0x01)
	
	py += 16
	u64 button_0x00 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.options.buttonselected, 0x00, "Erase all data")
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, button_0x00, 0x01, (BlueSpheresTimer.Records.writed() > 0x00 || BlueSpheresTimer.Emeralds.writed() > 0x00 || BlueSpheresTimer.Achievements.writed() > 0x00) ? 0xff : 0x90)
	
	py += 16
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, "-", 0x02, 0x90)
	
	py += 16
	u64 button_0x01 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.options.buttonselected, 0x01, "Erase all records")
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, button_0x01, 0x01, (BlueSpheresTimer.Records.writed() > 0x00) ? 0xff : 0x90)
	
	py += 16
	u64 button_0x02 = BlueSpheresTimer.TextManager.getMenuItem(bluespheres.options.buttonselected, 0x02, "Erase all achievements")
	BlueSpheresTimer.TextManager.drawText(px, py, 0xf000, 0x01, button_0x02, 0x01, (BlueSpheresTimer.Achievements.writed() > 0x00) ? 0xff : 0x90)
} 