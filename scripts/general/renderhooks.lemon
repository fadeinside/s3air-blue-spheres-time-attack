function bool Standalone.onWriteToSpriteTable(s16 px, s16 py, u16 renderQueue)
{
	u8 gameMode = global.game_mode & 0x7f
	
	if ((gameMode == 0x48 || gameMode == 0x0c) && SETTING_BS_TIME_BONUSES_ON_SPECIALS) // Blue Spheres results + fade to main game afterwards && check enabled Time bonu
	{
		// Blue Spheres results texts
		if (objA0.update_address == 0x02ea50)
		{
			py -= 0x10
			bool isSKStage = (global.lock_on_state == 0x00 && global.sk_bluespheres)
			
			if (objA0.animation.sprite == 0x19 || objA0.animation.sprite == 0x33 || objA0.animation.sprite == 0x38)
			{
				u64 key
				if (!Mods.isModActive("Sonic Mania HUD"))
					key = isSKStage ? "hud_text_time_superstage" : "hud_text_time_chaosstage"
				else
					key = "hud_text_time"
				
				Renderer.drawCustomSprite(key, px, py, 0x00, SPRITE_FLAG_PRIO, renderQueue)
				Renderer.drawCustomSprite(isSKStage ? "hud_text_bonus_superstage" : "hud_text_bonus_chaosstage", px + 0x28, py, 0x00, SPRITE_FLAG_PRIO, renderQueue)
				HUD.drawNumber(isSKStage ? "hud_digit_%d" : "hud_digit_%d_chaosstage", bluespheres.timer.bonus * 0x0a, px + 0xb8, py, renderQueue)
				Renderer.drawCustomSprite(getCharacterBonusTextIcon(getMainCharacter()), px + 0x4d, py - 0x05, 0x00, SPRITE_FLAG_PRIO, renderQueue)
				
				py += 0x10
				Renderer.drawCustomSprite(isSKStage ? "hud_text_perfect_superstage" : "hud_text_perfect_chaosstage", px, py, 0x00, SPRITE_FLAG_PRIO, renderQueue)
				HUD.drawNumber(isSKStage ? "hud_digit_%d" : "hud_digit_%d_chaosstage", results.time_bonus * 0x0a, px + 0xb8, py, renderQueue)
				Renderer.drawCustomSprite(getCharacterBonusTextIcon(getMainCharacter()), px + 0x34, py - 0x05, 0x00, SPRITE_FLAG_PRIO, renderQueue)
				return true
			}
			else if (objA0.animation.sprite == 0x1a || objA0.animation.sprite == 0x34 || objA0.animation.sprite == 0x39)
			{
				py += 0x18
				Renderer.drawCustomSprite(isSKStage ? "hud_text_continue_superstage" : "hud_text_continue_chaosstage", px, py, 0x00, SPRITE_FLAG_PRIO, renderQueue)
				Renderer.drawCustomSprite(getCharacterBonusTextIcon(getMainCharacter()), px + 0x3c, py - 0x05, 0x00, SPRITE_FLAG_PRIO, renderQueue)
				return true
			}
		}
	}
	
	// HUD
	if (objA0.update_address == 0x008e2a)
	{
		// This is the best I could come up with, for a better view. If I insert it inside the script in another place, it will be invisible for about one frame before it appears
		BlueSpheresTimer.renderTimer(px, py, renderQueue)
	}
	
	// Time over text
	if ((objA0.update_address == 0x008f24 || objA0.update_address == 0x008eec) && bluespheres.timeOver)
	{
		// "Not Enough Rings"
		if (bluespheres.rings_remaining != 0x00)
		{
			px += 0x02
			if (objA0.animation.sprite == 0x02) // Part 1
				Renderer.drawCustomSprite("Text_NotEnou", px, py, 0x00, 0x00, renderQueue)
			if (objA0.animation.sprite == 0x03) // Part 2
				Renderer.drawCustomSprite("Text_ghRings", px, py, 0x00, 0x00, renderQueue)
			return true
		}
		
		if (objA0.animation.sprite == 0x02) // Part 1
			Renderer.drawCustomSprite("Text_Time", px, py, 0x00, 0x00, renderQueue)
		if (objA0.animation.sprite == 0x03) // Part 2
			Renderer.drawCustomSprite("Text_Over", px, py, 0x00, 0x00, renderQueue)
		return true
	}
	
	return base.Standalone.onWriteToSpriteTable(px, py, renderQueue)
}